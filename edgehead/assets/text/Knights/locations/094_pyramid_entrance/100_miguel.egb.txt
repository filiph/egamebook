// ---
ACTION: $talk_to_miguel_greetings
COMMAND: Guardsman >> Talk >> "Greetings."

PREREQUISITES:
c.inRoomWith(miguelId)

INK:
// Subtext: the knight is taking himself seriously.
The knight nods.

"Welcome," he says, and there is a bit of sarcasm in his voice.

* "I am Aren."

* "What's your name?"

-

"Miguel."

// ---
ACTION: $talk_to_miguel_about_brother
COMMAND: Miguel, the guardsman >> Talk >> "I'm looking for a Sarn of Falling Rock."

PREREQUISITES:
c.inRoomWith(miguelId)
&&
w.actionHasBeenPerformed("talk_to_miguel_greetings")
&&
w.actionNeverUsed("talk_to_kat_about_brother")

INK:
// Subtext: The knight-man is frustrated that his "legacy" (defending the innocent) cannot be fulfilled. The woman just wants to provide strength where needed, doesn't want to fight if not necessary.
"Sarn? Doesn't ring the bell. Who is he?"

* "He came here to join the Knights."

    Miguel looks surprised. "He's a warrior, then?"

    * * "No, a blacksmith."

        "Ah. He's a mender with the Knights." 

* "A blacksmith."

    "There are no blacksmiths here." Miguel gestures around, towards the ruins and the forest.

    * * "Whose work is your sword, then?"

        "Her?" He looks at his sword and pats it. "She's not from here. I bought her in Lywood, years ago, for more coins that I will admit." He smiles for the first time since we met.

        * * * "The sword looks great."

            Miguel's smile widens. "You get what you pay for. And I'm taking good care of her, taking her to ..." Miguel stops. "Oh!" He shakes his head, still smiling.

        * * * "Who repairs it when there's a chink in the blade?"

            "Oh!" he says. 

        - - -

        "I know what you're driving at. You mean a mender. Your Sarn is a mender with the Knights."

    * * "The Knights recruited him."

        "Oh, you mean a mender."

-

"Well, I don't know of a mender called Sarn."

[[RULESET]]
[[RULE]]
c.inRoomParent('pyramid_entrance')
&&
c.inRoomWith(katId)
[[THEN]]
// This must be one paragraph until the following bug is fixed:
// https://trello.com/c/UrXXOYJJ/1223-bug-cannot-have-a-blank-line-in-a-ruleset-when-in-ink
Miguel looks over his shoulder at the Pyramid, then back at me. "Even if he is in there, you would not want to get in. You would want to get out."
The woman looks at him with a mix of puzzlement and exasperation, then she turns to me.
"This place is no longer safe. Orcs, goblins. Unless you have business with one of the farmers, you shouldn't go in."
[[ENDRULE]]
[[RULE]]
$DEFAULT
[[THEN]]
// Nothing.
[[ENDRULE]]
[[ENDRULESET]]



// ---
ACTION: $talk_to_miguel_about_devling
COMMAND: Miguel, the guardsman >> Talk >> “Any advice for someone delving in the Pyramid?”

PREREQUISITES:
c.inRoomWith(miguelId)
&&
w.actionHasBeenPerformed("talk_to_miguel_greetings")

INK:
"[[IF w.actionHasBeenPerformed("talk_to_kat_about_devling")]]What Kat said. I'd just add that[[ELSE]]You should probably ask Kat. She's the smarter and more experienced of us two. But I'll say that[[ENDIF]] I've heard locals talk of a device of war called the Dragon Egg. If I were you, no offense, I'd try to find any advantage possible against the Orcs and the other creatures of the upside."

[[CODE]]
c.learn(OrcsFacts.inPyramid);
c.learn(DragonEggFacts.anAncientWeapon);
c.learn(DelvingFacts.infoHelps);
[[ENDCODE]]


// ---
ACTION: $talk_to_miguel_about_dragon_egg
COMMAND: Miguel, the guardsman >> Talk >> “What do you know about the Dragon Egg?”

PREREQUISITES:
c.inRoomWith(miguelId)
&&
w.actionHasBeenPerformed("talk_to_miguel_greetings")
&&
c.knows(DragonEggFacts.anAncientWeapon)

INK:
"It's an ancient weapon, somewhere in the Pyramid." He shrugs. "If I knew more, I'd go find it, [young sir]."


// ---
ACTION: $talk_to_miguel_about_lady
COMMAND: Miguel, the guardsman >> Talk >> “Who's that lady up there?”
FOR_LOCATION: $pyramid_entrance

PREREQUISITES:
c.inRoomWith(miguelId)
&&
w.actionHasBeenPerformed("talk_to_miguel_greetings")
&&
c.knows(LadyHopeFacts.ladyInKeep)
&&
!c.knows(LadyHopeFacts.ladyHopeName)

INK:
"Lady Hope."

[[CODE]]
c.learn(LadyHopeFacts.ladyHopeName);
[[ENDCODE]]
