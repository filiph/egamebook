// ---
ACTION: $talk_to_miguel_greetings
COMMAND: Guardsman >> Talk >> "Greetings."

PREREQUISITES:
c.inRoomWith(miguelId)

INK:
// Subtext: the knight is taking himself seriously.
The knight nods.

"Welcome," he says, and there is a bit of sarcasm in his voice.

* "I am [Aren]."

* "What's your name?"

-

"Miguel."

// ---
ACTION: $talk_to_miguel_about_brother
COMMAND: Miguel, the guardsman >> Talk >> "I'm looking for a Sarn of Falling Rock."

PREREQUISITES:
c.inRoomWith(miguelId)
&&
c.inRoomWith(katId)
&&
w.actionHasBeenPerformed("talk_to_miguel_greetings")
&&
w.actionNeverUsed("talk_to_kat_about_brother")

INK:
// Subtext: The knight-man is frustrated that his "legacy" (defending the innocent) cannot be fulfilled. The woman just wants to provide strength where needed, doesn't want to fight if not necessary.
"Sarn? Doesn't ring the bell. Who is he?"

* "He came here with the Knights."

    Miguel looks surprised. "He's a knight, then?"

    * * "No, a blacksmith."

        "Ah. He's a mender with the Knights." 

* "A blacksmith."

    "There are no blacksmiths here." Miguel gestures around, towards the ruins and the forest.

    * * "Whose work is your sword, then?"

        "Her?" He looks at his sword and pats it. "She's not from here. I bought her in Lywood, years ago, for more coins that I will admit." He smiles for the first time since we met.

        * * * "The sword looks great."

            Miguel's smile widens. "You get what you pay for. And I'm taking good care of her, taking her to ..." Miguel stops. "Oh!" He shakes his head, still smiling.

        * * * "Who repairs it when there's a chink in the blade?"

            "Oh!" he says. 

        - - -

        "I know what you're driving at. You mean a mender. Your Sarn is a mender with the Knights."

    * * "The Knights recruited him."

        "Oh, you mean a mender."

-

He looks to the woman. "Do you know of a mender called Sarn?"

"No," she says.

* "But he might be in."

-

Miguel looks over his shoulder at the Pyramid, then back at me. "Even if he is, you would not want to get in. You would want to get out."

The woman looks at him with a mix of puzzlement and exasperation, then she turns to me.

"This place is no longer safe. Unless you have business with one of the farmers, you shouldn't go in."
